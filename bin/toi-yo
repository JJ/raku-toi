#!/usr/bin/env raku

# Example telegram bot that uses the module for checking in

use Telegram; # remember to zef install Telegram
use Toi;
use Toi::Data::Files;

sub MAIN( $file-prefix = "agil" ) {

    say "Ficheros con prefijo $file-prefix para almacenar check-ins";
    die "No token" unless %*ENV<TOI_BOT_TOKEN>;
    my $bot = Telegram::Bot.new(%*ENV<TOI_BOT_TOKEN>);
    my $data = Toi::Data::Files.new;
    my $toi = Toi.new: :$data;
    $bot.start(1);
    react {
        whenever $bot.messagesTap -> $msg {
            $msg.text ~~ /\/$<command> = (\w+) \h* $<args> = (.*)/;
            say $msg.raku, " → ",  $<command>;
            given $<command> {
                when "toi" {
                    my $nombre = $msg.sender.firstname ~ " " ~ $msg.sender.lastname;
                    $toi.check-in($msg.sender.username, $nombre );
                    $bot.sendMessage($msg.chat.id,
                                     "✓ \@{$msg.sender.username} asiste como «$nombre»" ) ;
                }
                when "about" {
                    $bot.sendMessage($msg.chat.id, q:to/EOM/);
Para registrarte, usa simplemente /toi
EOM
                }
            }
        }
        whenever signal(SIGINT) {
            $bot.stop;
            exit;
        }
    }
}
